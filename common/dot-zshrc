function zcompile-many() {
    local f
    for f; do
        if ! [[ "$f" =~ "\.zwc$" ]]; then
            zcompile -R -- "$f".zwc "$f"
        fi
    done
}

# Clone and compile to wordcode missing plugins.
if [[ ! -e ~/zsh/fzf ]]; then
    git clone --depth=1 https://github.com/junegunn/fzf.git ~/zsh/fzf
    ~/zsh/fzf/install --bin
    cp ~/zsh/fzf/bin/fzf ~/bin
fi
if [[ ! -e ~/zsh/zsh-syntax-highlighting ]]; then
    git clone --depth=1 https://github.com/zsh-users/zsh-syntax-highlighting.git ~/zsh/zsh-syntax-highlighting
    zcompile-many ~/zsh/zsh-syntax-highlighting/{zsh-syntax-highlighting.zsh,highlighters/*/*.zsh}
fi
if [[ ! -e ~/zsh/zsh-autosuggestions ]]; then
    git clone --depth=1 https://github.com/zsh-users/zsh-autosuggestions.git ~/zsh/zsh-autosuggestions
    zcompile-many ~/zsh/zsh-autosuggestions/{zsh-autosuggestions.zsh,src/**/*.zsh}
fi
if [[ ! -e ~/zsh/zsh-completions ]]; then
    git clone --depth=1 https://github.com/zsh-users/zsh-completions.git ~/zsh/zsh-completions
fi
if [[ ! -e ~/zsh/zsh-history-substring-search ]]; then
    git clone --depth=1 https://github.com/zsh-users/zsh-history-substring-search.git ~/zsh/zsh-history-substring-search
    zcompile-many ~/zsh/zsh-history-substring-search/*.zsh
fi
if [[ ! -e ~/zsh/powerlevel10k ]]; then
    git clone --depth=1 https://github.com/romkatv/powerlevel10k.git ~/zsh/powerlevel10k
    make -C ~/zsh/powerlevel10k pkg
fi

function env_compile() {
    zcompile-many ~/env/*.zsh
    zcompile-many ~/env/completions/_*
    if [ -e ~/env/overrides/ ]; then
        zcompile-many ~/env/overrides/*.zsh
    fi
}

if [[ ! -e ~/env/functions.zsh.zwc ]]; then
    env_compile
fi

# Enable the "new" completion system (compsys).
source ~/env/env-vars.zsh
zmodload zsh/complist
zstyle ':completion:*' menu select
autoload -Uz compinit && compinit

# Load plugins.
source ~/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source ~/zsh/zsh-autosuggestions/zsh-autosuggestions.zsh
source ~/zsh/zsh-history-substring-search/zsh-history-substring-search.zsh
source ~/zsh/powerlevel10k/powerlevel10k.zsh-theme
source ~/.p10k.zsh
source <(fzf --zsh)
source ~/env/functions.zsh
source ~/env/aliases.zsh
source ~/env/misc.zsh

[[ ~/.zcompdump.zwc -nt ~/.zcompdump ]] || zcompile-many ~/.zcompdump

# vim: ft=zsh
